label day3:

  "..." """
  (…)rong accident. Three involved. Two injured. One of them is in a grave state. There was also a deceased. They’ll be transfered to(…)
  """

  scene bg dark
  with Fade(0.5, 2.0, 0.5)

  "..." """
  (…) Yes. One of the involved died. One of the wheels of the car decapitated him while trying to avoid him

  Yes, in the shot it showed how two people entered the path from nowhere, and after the accident there was only one.

  We don't have an identification of the dead one, only of the ones inside the car... Come with me outside the room.
  """

  scene bg dark
  with Fade(0.5, 2.0, 0.5)

  "..." """
  (…) has suffered a series of traumas in less than a day, the worst is that this guy is involved in all of them.

  He is pathetic. I don't know how the fiscal thought it was a good idea to leave her with him
  """

  scene bg dark
  with Fade(0.5, 1.0, 0.5)

  "..." """
  (…) No. The girl had no one here and the only person with who she spoke last night was him

  The fiscal thought that she'd be better with someone as close as him and, despite what happened it, he thougth that this dude could be. 

  This girl has a luck for the sh… 

  Wait, he’s awakening
  """

  scene bg hospital room
  with Fade(2.0, 1.0, 2.0)

  show kyoko normal at center
  with dissolve
  show kyoko speaking serious
  with dissolve

  play music solo_music fadein 1.0 fadeout 1.0 loop

  "..." "Good afternoon, Professor"
  show kyoko normal

  show alan young normal at left:
    xalign 0.2
  with dissolve
  show alan young serious talk
  with dissolve

  show kyoko normal:
    xalign 0.8
  with move

  "..." "Less than a day. Less than a damn day and look what you did"

  show alan young normal
  show kyoko speaking serious

  "..." "Officer, please. Is not the moment."
  kyoko """
  My name is Kyoko, Professor. I am a Lawyer assigned to you

  Due to all that has happened since yesterday, things has escalated to a really bad point
  """

  show kyoko normal
  show alan young serious talk

  "..." "Too fucking fast, I'd say"

  show alan young listening serious
  show kyoko speaking serious

  kyoko "Officer, Please. I'd have to ask you to leave us alone for a moment"

  show kyoko normal
  show alan young loud speaking

  "..." "Five minutes. No more."

  play sound door_close

  hide alan young loud speaking
  with dissolve

  show kyoko normal at center
  with move
  show kyoko speaking serious
  with dissolve

  kyoko """
  Look, I'm gonna be sincere with you

  This looks really bad. Kanae is in coma

  The officer Alan wants your head right now
  """

  show kyoko normal serious

  "So, his name is Alan...{w=0.4} I didn't knew his name until now"

  show kyoko normal speaking

  kyoko """
  I don't know what happened in that house with Makawa, I also don't know what has happened with you and Kanae

  So I want you to be clear with me.

  All this case looks really misterious... why Kanae is with you? How did you ended up in that road?
  """

  show kyoko normal

  me """
  We went to see a friend of mine

  I though he could help Kanae in all that she is going throug
  """

  "I couldn't tell her everything, I'd be sent to a asylum in an instant"

  me "When we were returning, something happened on the road"

  show kyoko normal speaking

  me "Someone...{w=1.0} someone appeared in the road"

  show kyoko speaking serious

  kyoko "Appeared? From where?"

  show kyoko normal speaking

  me "I don't know. They where just there in a second"

  show kyoko normal

  me """
  I tried to avoid them, but the car failed and then we crashed.
  """

  show kyoko speaking serious

  kyoko """
  There were cameras in the road. In one of the lamp post

  The police is investigating them, but kinda looks like they came from nowhere.

  The image is in a really bad state, but is our best defense

  I want you to know that there was a dead person... In the accident, you killed one of the two that appeared 
  """

  show kyoko normal

  "I killed someone again?"
  "I look at my finger and I notice that there's another space in the scarred ring filled."

  show kyoko speaking serious

  kyoko "Alan is taking that to put you as the biggest threat that this town has faced"

  show kyoko normal

  me "I didn't do it because I wanted. It was an accident"

  show kyoko speaking serious

  kyoko "I know, but he doesn't care"

  show kyoko speaking

  kyoko """
  There's no information about him in the system, we just know that it was a man between twenty five and twenty eight years.

  You took his head with the wheel of your car
  """

  show kyoko normal

  me "And the other one? I remember there were two of them"

  show kyoko speaking
  kyoko "We don't know. He or she just vanished just like they appeared."

  show kyoko normal:
    xalign 0.8
  with move

  show alan young normal at center
  with dissolve
  play sound door_close
  show alan young serious talk
  with dissolve

  alan "Well... the man needs to rest a little bit."

  show kyoko speaking serious
  kyoko "You don't have to say it."

  show kyoko normal
  show alan young serious talk

  alan "Calm down. He'll be fine. I just want to protect you from him"

  show kyoko normal speaking

  alan "This is a really dangerous man. He has killed two persons in two days. I don't want you to be the third one."

  show alan young normal

  show kyoko speaking serious
  kyoko "You don't have anything to say that, Officer Alan. Be careful with your words."
  kyoko "This is an accident that you want to escalate just because you want someone in jail."

  show kyoko normal
  show alan young malicious smile

  alan "You said that as if I was the bad guy here."

  show alan young serious talk
  with dissolve

  alan "Remember, wasn't me who took the family of a kid and then put her in a coma."

  show alan young normal

  """
  They are talking as if I wasn't here.
  """

  me "That was an accident, Officer"

  show kyoko speaking serious
  kyoko "You don't have to say anything. We will prove that to the Officer. Who seems really worried about what's happening"

  show kyoko normal

  show alan young malicious smile
  alan "I am just worried that the law is respected"

  show alan young smiling
  alan "Anyway, he needs to rest, don't you thing?"

  show alan young mailicious soft smile
  show kyoko speaking serious

  kyoko "Yes. We should both leave."
  kyoko "I'll be back later, Professor."

  show kyoko normal
  with dissolve

  hide kyoko normal
  with dissolve
  play sound door_close

  "Kyoko leaves, but Alan stays in the room for a second"
  "She didn't notice that he stayed"

  show alan young speaking
  with dissolve

  alan """
  It doesn't matter if it was your fault or not, really

  I'm not interested if the guy you decapitated appeared spontaneously in front of you 

  You screwed up that girl's life. You only had to protect her for a few weeks until they got someone better, but look how it ended 
  """

  show alan young serious talk
  with dissolve

  alan """
  She is in continuous observation. She is in a coma since yesterday. The doctors don't know why

  They think it may be due to a trauma in her head caused by the crash

  On the other hand, the guy you killed is not identified anywhere...{w=1.0} you killed a nobody, just like you
  """

  show alan young listening serious
  with dissolve

  alan """
  Look me in the eyes, bum

  In one day you finished destroying that girl's life

  From here you will not go straight to your house like yesterday. I can assure you that
  """

  show alan young normal
  hide alan young normal 
  with dissolve
  play sound door_close

  stop music fadeout 4.0

  """
  Without being able to say anything at all, he leaves my room with a totally sure threat that when I heal, my ass will end up in prison

  I had a few wounds on my arms and forehead. My legs are hurt as well

  I could see the time on the clock that hung on the wall of my room
  """

  scene bg hospital room distorted
  with dissolve

  """
  I try to get out of bed, still having an incredible headache and watching the floor turn towards the ceiling due to how dizzy I was.
  """

  scene bg hospital corridor distorted
  with dissolve

  play sound bones

  """
  I manage to leave my room with difficulty while staggering down the desolate corridor at noon

  There were no officers guarding the entrance in case I escaped, there were no nurses nearby. It seemed like I was alone in this hospital
  """

  scene bg hospital corridor
  with dissolve

  play music dramatic fadein 1.0 fadeout 1.0 loop

  """
  I am in front Kanae's room.

  She is lying in that bed with all the devices connected to her body

  Fuck. Yesterday was a disaster...

  All this has been a disaster, and all because of this ring

  I put my hand on the glass, wanting to reverse all this tragedy that has happened
  """

  menu:
    "This scar seems like"

    "Life of people":
      $ day3_clear_mind = True

    "Days that has passed":
      $ day3_clear_mind = False

  if day3_clear_mind:
    """
    Everytime someone has died since I got this, a space in the ring was marked

    There was seven of them, now there are five.

    But then, Itawa didn't count as one? Or it counted as one along with Makawa?

    I don't understand
    """
  else:
    """
    Since the event in Kanae's room, two days has passed

    With each day, a new event happens that involves something with time and space.

    Maybe this ring is the one making all this... or maybe I already lost my mind.
    """

  """
  While thinking on all that,  comes to my mind some things that they commented when I was semi conscious

  {i}Yes, in the shot it showed how two people entered the path from nowhere, and after the accident there was only one.{/i}

  {i}I'm not interested if the guy you decapitated appeared spontaneously in front of you{/i}

  I had fallen into a time loop before the accident, but they appeared from nowhere when I recovered my vision.

  I need to see the guy who died to make sure. I need to know who I hurt this time
  """

  show kyoko normal speaking at center
  with dissolve
  with hpunch

  kyoko "What the hell are you doing here?"

  "Dammit"

  show kyoko speaking

  kyoko """
  How did you leave your room? 

  You shouldn't even be able to move

  Your wounds are in very bad condition. Let me to take you back to your bed, you need to rest
  """

  show kyoko normal

  me """
  You don't understand it. I have to see the body of this person

  It may seem like something out of a totally twisted idea, but after everything that had happen these past few days, I have to see who I hurt this time 
  """

  """
  I couldn't say why I really wanted to see him.
  
  I don't dare to rule anything out

  Less if there are indications that the death of this guy wasn't a simple accident
  """

  show kyoko normal speaking

  kyoko """
  Listen, I know what's on your mind. You feel guilty for all this. You have your reasons.

  Kanae was your responsability and you couldn't protect her as you wanted, but you can't do this right now.

  Not with Alan breathing in your neck watching all the things that you do or say
  """

  show kyoko normal

  """
  She is right

  With my arm over her shoulder and supporting me, she leads me back to my room in the hospital and helps me to lie on the bed

  I feel that every bone in my body is cracked.
  """

  hide kyoko normal 
  with dissolve

  scene bg hospital room
  with fade

  stop music fadeout 1.0

  """
  I am observing the ceiling that reflects an empty light which absorbs my notion of time

  Ironic

  Actually I don't know if this really happens normally or I'm stuck in another temporary loop or if everything just stopped
  """

  show fly:
    xalign 0.2 yalign 0.1 rotate 80
  with dissolve

  python:
    renpy.pause(delay=0.5)

  show fly:
    xalign 0.7 yalign 0.3 rotate 45
  with move

  show fly:
    xalign 0.4 yalign 0.8 rotate 176
  with move

  python:
    renpy.pause(delay=0.7)

  show fly:
    xalign 0.5 yalign 0.5 rotate 250
  with move

  """
  No, time goes on
  """

  show fly:
    xalign 0.2 yalign 0.3 rotate 30
  with move

  hide fly
  with dissolve

  """
  It's relaxing. Know that time is passing. To know that I am not in a void without end and without beginning

  Knowing that I am not in the temporary nonexistence that I have experienced previously, it reassures me, but not enough as I'd like

  There are still things that I must understand, and I know that see who was the new victim of my disaster will help me solve all this

  I need to see him
  """

  scene bg hospital room distorted
  with dissolve

  play music solo_music fadein 3.0 fadeout 1.0 loop 

  play sound bones

  """
  I get up again from my bed feeling as if my bones were splintering one by one
  """

  scene bg hospital corridor
  with dissolve

  """
  I've seen just one person beside Kyoko and Alan in this hospital.

  For sure is Alan who is preventing them from taking care of me

  When I left the room and went to the morgue, I saw some nurses busy in the corridor. They didn't realized my presence
  """

  scene bg hospital morgue
  with fade

  """
  There are a few bodies here. I am exhausted.

  One of them was just in front of me, so holding my breath, I lifted the blanket that covered it
  """

  #waaaa sound

  """
  Although I thought I was ready to see this guy, I wasn't ready to see a totally missing head

  Only a part of the tongue is coming out of what was left of his neck

  The scene made every part of my stomach go back through my esophagus until I vomited it
  """

  scene bg dark
  with fade

  $ renpy.music.set_volume(0.0, delay=1, channel='music')

  "I think I fainted again"

  scene bg hospital room
  with fade

  alan """
  What the hell was he doing there? Isn't this hospital supposed to have surveillance? 
  """

  """
  Those screams... that's Alan.
  """

  $ renpy.music.set_volume(1.0, delay=3, channel='music')

  alan """
  Do I have to stand guard myself? This guy is extremely dangerous

  Yesterday he decapitated a guy on the road leaving a child in a coma and before yesterday killed the same girl's brother

  I don't care who you have to put here to watch him, just do it
  """

  show alan young mad
  with dissolve
  with hpunch

  play sound door_close

  alan """
  What the hell are you planning? Are you crazy? 

  Actually, doesn't matter anymore, right now you'll go straight to prison
  """



  show alan young speaking

  alan """
  I want you to know that this is not because I hate you or anything like that

  It's only that you did everything in your power to ruin that girl's life.

  You just had to take care of her. See for her. You got rid of her shitty brother, but no

  In less than a day you left her in intensive care. Less than a damn day. That's what I don't accept
  """

  show alan young normal

  """
  What would he think if I told him what happened? 

  I could tell him everything, even if it makes me look like a crazy

  Unless I tell him only the part he knows
  """

  stop music fadeout 5.0
  menu:
    "Should I tell him?"

    "Yes":
      $ day3_told_alan = True

    "No":
      $ day3_told_alan = False

  if day3_told_alan:

    play music main fadein 1.0 fadeout 1.0 loop

    show alan young listening serious
    me """
    Alan...

    I know that this seems messed up, but is not my fault.

    Something has happened these few days. Since Makawa, everything has been so weird.

    And it came clear to me when those people appeared out of nowhere in the road
    """

    show alan young mailicious soft smile
    with dissolve

    me "I need your help. I need to understand what's happening"

    show alan young mad

    alan """
    Do you think I'm an idiot?

    Did you really believe that I would believe this?

    You are trying to hold the last scraps of information you can to create an escape route.

    Even though you have the shit up your nose, you still try to breathe clean air.
    """

    show alan young loud speaking

    alan """
    Of course I saw that video. I have no idea what happened on that road. Those two persons came out of nowhere in an instant

    But still if you had not embarked on that childish expedition with her to find who knows what, the girl wouldn't be in that condition
    """
  else:
    play music dramatic fadein 4.0 fadeout 2.0 loop

  show alan young serious talk

  alan """
  You'll have surveillance until we come to take you out

  I would do it myself, but you have the unfortunate luck that nobody is to cover me. 

  As for the girl, I'll see what I can do for her
  """

  hide alan young serious talk
  with dissolve

  #door sound

  if day3_told_alan:
    """
    I don't know what I expected by telling him this
    """

  """
  Kyoko says that I shouldn't leave the room, Alan wants me to try it so he can send me more easily to jail

  Damnit. If only I could get rid of this policeman who took me as his main objective 

  I don't know how yet, but I know that I can prevent Kanae from living in this reality, I am convinced of this
  """
  
  stop music fadeout 3.0

  """
  Previously I've been able to move through time

  That happened when I confronted Makawa. 

  That made me....{w=1.0} kill Itawa

  But at that moment I moved between dimensions unintentionally, as in yesterday's accident

  I entered in this empty space without intention and things happened. Things that I can't control. 

  If only I could stop the course of time on my own free will even this time.
  """

  play music solo_music fadein 2.0 fadeout 2.0 loop

  show kyoko normal at center
  with dissolve
  play sound door_close

  show kyoko normal speaking
  with dissolve

  kyoko "What the hell are you planning?"

  show kyoko speaking serious

  "Now she's going to punch me"

  kyoko "I really want to understand, but you aren't leaving it easy for me"

  show kyoko normal

  me "You wouldn't understand"

  show kyoko speaking serious

  kyoko "Test me."

  show kyoko normal

  if day3_told_alan:

    call day3_did_told_alan from _call_day3_did_told_alan

  else:

    call day3_not_told_alan from _call_day3_not_told_alan

  stop music fadeout 5.0
  play sound wind fadein 1.0 fadeout 3.0

  $ renpy.pause(delay=2.0)

  stop sound fadeout 4.0

  """
  The pain in my head starts to increase

  The pressure in my chest is so great that I feel as if it breaks me

  I can't lose this. I have to focus

  With my eyes tightly closed, in less than a second, I stop feeling everything

  I feel suspended in nothingness. Where am I?
  """

  scene bg dark 
  with Fade(0.5,1.0,0.5)

  scene bg hospital room
  with Fade(1.0, 3.0, 1.0)

  """
  I feel nothing around me. I'm in the room... it worked?

  I get up slowly from the bed. It is as if I didn't feel my steps, I can walk freely

  Every pain in my bones just disappeared

  There were no noise no matter how much I talked
  """

  if not day3_told_alan:
    show kyoko normal serious
    with dissolve

    """
    Kyoko is still here, but she doesn't move

    I get close to her nose, she is not breathing

    It really worked

    I must hurry, I don't know how long this will last
    """

    hide kyoko normal serious
    with dissolve
  else:

    """
    I get close to the door

    I can pull the door of my room with all the force and when I release it, it will stay static where I left it

    I must hurry, I don't know how long this will last
    """

  scene bg hospital corridor
  with fade

  if day3_told_alan:
    show kyoko normal serious
    with dissolve

    """
    There was Kyoko outside my room, she seems worried.
    """

    hide kyoko normal serious
    with dissolve
  else:
    show alan young normal
    """
    There was Alan in the corridor. He doesn't move
    """

    hide alan young normal
    with dissolve

  """
  The morgue is not far from my room, I know how to get there and I'm a bit more prepared to see the body of this guy

  He has something, I know I've seen elsewhere

  I walk  through the corridors of this hospital, seeing how my surroundings are completely stopped

  Unfinished steps to give, birds without finishing their flutter
  """

  #tuku tuku tuku... that's a heart

  """
  Water drops that doesn't touch the ground and the sordid silence that accompanied me where I can't hear nor the beats of my own heart
  """

  scene bg hospital morgue
  with fade

  #TOKU TOKU... intense heart 

  "I feel pressure in my chest... I don't have much time"

  $ renpy.music.set_volume(0.3, channel='music')

  play music solo_music fadein 1.0 fadeout 1.0 loop

  """
  I slowly uncovered the decapitated corpse to visualize his neck, or what was left of it
  """

  show tattoo:
    xalign 0.5 yalign 0.5
  with dissolve

  """
  I know where that tattoo is from.

  Oyab had one as well. Of course, this body is more clean than him at that time.

  It can't be, we left him behind when he closed the door in our face

  It's impossible for this to be his body. Although he appeared out of nowhere...

  How the hell did he get there? And most importantly, who the hell appeared out of nowhere with him?

  It's impossible that he got there first, we where in the middle of nowhere

  I need to find the other person that appeared with him, is the only one who can give me an explanation about what happened.

  Perhaps I am not the oly one who travels between realities, or at least in slices of time

  Damn, three days ago I would have thought that such a comment would only be part of a science fiction story written by a middle age dude with his laptop in his stomach.

  I take it as a great possibility now. I have to get out of here.
  """

  if day3_told_alan:
    "I am sorry, Kyoko. I know you wanted to help me, but this is beyond us."

  $ renpy.music.set_volume(0.7, delay=2.0, channel='music')

  """
  I can't take Kanae with me, I don't know how much I'd be able to hold time

  She'll be more safe here
  """

  show red_layer at center:
    alpha .2 xalign 0.5 yalign 0.5
  with dissolve

  $ renpy.pause(delay=0.5)

  hide red_layer
  with dissolve

  """
  Time is running out, I have to hurry up and get out before it's too late
  """

  scene bg hospital corridor
  with fade

  """
  When leaving the morgue, something begins to change

  I see a drop falling from the air conditioning vent as if it were in slow motion

  I start running while I stumble over all the obstacles visible because of the imbalance I feel

  When each table stumbled, when the objects fell, they went in slow motion in the direction that I had pushed them
  """

  scene bg hospital front
  with fade 

  """
  I leave the hospital with the greatest of my haste

  When I open the exit door I can contemplate this panorama only for a few seconds

  The wind moving the branches of the trees at a speed that allows me to appreciate each one of the leaves in them.
  """

  show red_layer:
    alpha .4 xalign 0.5 yalign 0.5
  with dissolve

  #auch

  $ renpy.pause(delay=0.4)

  hide red_layer
  with dissolve

  $ renpy.music.set_volume(1.0, delay=2.0, channel='music')

  """
  As I give the next step to leave the hospital,  a stitch in the center of my forehead makes me fall on the floor with an inmesurable pain
  """

  scene bg dark
  with fade

  """
  Tears in my eyes that burst from the pressure I felt

  I can't stand up
  """

  scene bg hospital front
  with fade

  """
  I start to crawl on the ground in order to leave that place

  As I stand up and look around, I see that everything is as if nothing had happened

  The whole course of time return to its usual rhythm right in front of me

  I need to go where the accident happened
  """

  scene bg town evening
  with fade

  """
  I walked among the people looking like a homeless

  There where a few cops, but nothing serious.
  """

  "..." "It can't be. I can't believe it!"

  "This voice...{w=1.0} oh, no. Damnit"

  show supervisor smiling
  with dissolve
  with hpunch

  supervisor "Colleague!"
  me "Hi...{w=0.5} em, sorry. I don't have time, really."

  show supervisor speaking
  supervisor "I knew what happened with Makawa, very bad thing, bud."

  supervisor "By the way, why are you looking like that?"

  show supervisor normal

  menu:
    "I need to get out of here"

    "I need your help":
      $ day3_ask_help = True
      call day3_asked_for_help from _call_day3_asked_for_help

    "Leave":
      $ day3_ask_help = False
      call day3_not_asked_for_help from _call_day3_not_asked_for_help

  "I leave, while the cops starts chasing me"

  stop music fadeout 1.0

  scene bg road night 
  with fade

  """
  I finally am here.

  Here it was where it happened. I can see a big blood stain

  Right there was where these people appeared... but now there's nothing.

  Only desolate way
  """

  $ renpy.music.set_volume(0.4, channel='sound')

  play sound police fadein 3.0 loop

  scene bg road night police 
  with dissolve

  "They found me"

  #burn sound

  """
  It burns!

  My hand begins to burn again... the scar
  """

  if day3_clear_mind:
    """
    The Supervisor probably died. Another victim

    Another victim because of me and there is nothing here!
    """
  else:
    "I am crazy... All what I have done and for nothing"

  #sirens closer

  if not restart:
    python:
      renpy.notify("(:")

  "..." "Professor..."

  """
  That voice...

  No. Can't be.

  My silence shows my confusion, I stop working, and I am only contemplating it in an uncertainty that destroys my mind only by leaving her name
  """

  show kanae_alt normal
  with dissolve
  show kanae_alt speaking
  with dissolve

  my_kanae "We have to leave."

  show kanae_alt normal

  stop sound fadeout 3.0

  scene bg dark
  with Fade(1.0, 3.0, 1.0)
  $ renpy.music.set_volume(1.0, channel='sound')

  call day4 from _call_day4
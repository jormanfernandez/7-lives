label day6:

  me "Who are you?"

  show alan old speaking

  "..." "You don't recognize me, do you?"

  show alan old normal

  me "What do you want? We don't have anything"
  me "Please, let us go."

  show alan old speaking

  "..." """
  Time travel. An interesting topic, isn't it? 

  You see, when I was young I liked those ideas

  To think that there could be worlds where I wasn't in the police. Worlds where maybe I was president

  Any option was available.
  """

  show alan old smiling speaking closed eyes

  "..." """
  It was impossible to think that we could reach such a level

  Going from one moment to another in space time existed only as a mere thought that served as a distraction for our mind, but then you came
  """

  show alan old serious

  "..." """
  Seven years pursuing you, Seven years looking for you.
  """

  hide my_kanae worried soft
  with dissolve

  "It can't be"

  show alan old smiling closed eyes at center
  with move

  me "No... Alan?"

  show alan old sigh

  play music dramatic fadein 2.0 fadeout 2.0 loop

  alan """
  After destroying a lot of cars in the desert and causing a great disaster in the city by killing my men, you simply disappeared for seven years

  The funny thing is that time hasn't affected you. That happened just a few hours ago for you, isn't it?
  """

  show alan old serious

  alan """
  Kanae is the same. You're the same, and that monster with the same appearance of her I don't know where the hell it came from.

  The recordings that were left in the hospital showed you when you took Kanae away

  The explosion of the glass that happened out of nowhere and you being thrown to the ends of the room where she was
  """

  show alan old speaking

  if day3_told_alan:
    alan """
    You tried to explain it to me in the hospital, but I didn't belived in you.

    It couldn't be that what you implied in the hospital the day after Kanae's accident was true, but here we are now. It was.
    """
  else:
    alan """
    All this time I tried to give it an answer.

    The ones that appeared from nowhere in the road the day you had the accident with Kanae. All makes sense now.
    """

  show alan old smiling closed eyes
  alan """
  I think you may know this house.

  A small girl lived here. She left after that day.
  """

  show alan old speaking

  alan """
  I was able to get this house. Waiting in time.
  """

  show alan old serious

  alan """
  That thing killed a lot of my men that last day, you know?

  With what happened in the desert, many were seriously injured. A large part of them didn't make it.

  I stopped being in the police since that day

  I dedicated myself to look for you, but the earth... time swallowed you.
  """

  show alan old smiling closed eyes

  alan "The great irony of all this? Today I had given up with you"

  show alan old speaking
  alan """
  I was with the few friends that were left. And suddenly you three appeared. Kanae, you and that monster.

  All my colleagues recognized the three of you , and that, your friend, recognized me instantly

  What seven years ago had stopped, that thing restarted it
  """

  show alan old serious

  alan """
  Before bringing you here, your friend killed two of my friends. I don't know how it did it, but I know it was that thing

  Their hearts simply stopped. I could stop it by hitting its head quite hard

  It seems it was worried about taking care of you.
  """

  show alan old speaking
  alan """
  Now I know that I can contain it if it doesn't know where it is

  If it doesn't hear, doesn't see, if it doesn't feel

  If I remove it from the world, it can't do anything. So now we have a great time to talk.

  Don't worry, Kanae told me that you also have certain skills. So if you try something against me, you'll all die.

  I just closed that door, and if my heart for some reason stops beating, this whole house explodes
  """

  show alan old smiling closed eyes

  alan """
  Lovely, isn't it?
  """

  me """
  Alan, please. I know that what has happened has destroyed your life. Mine as well.

  It also destroyed Kanae's life.
  """

  show alan old serious

  alan "Yeah, it was because of you that this started"

  me """
  This is beyond us, beyond you and me.

  Its much more greater than anything we could think
  """

  show alan old sigh
  alan """
  You destroyed this girl life, that's pretty clear.

  But how that thing that looks like her joined this party?
  """

  show alan old normal:
    xalign 0.8
  with move

  show my_kanae worried soft at left:
    xalign 0.2
  with dissolve

  my_kanae """
  I'm sorry, Professor

  I don't know what to do
  """

  show my_kanae sad
  with dissolve

  my_kanae """
  I don't understand what's happening to me, what's happening to you.
  """

  """
  It seems that she doesn't fully remember what happened before the accident.
  """

  me "I don't know what Alan told you, but we are going to get out of here."

  show alan old speaking
  alan "Woah... watch out with your words, remember that you are the captive."

  show alan old serious

  alan """
  The day you killed Makawa, everything was very odd

  That your call has been delayed so that we couldn't attend to it in time

  That Makawa prepared all that scenario for you to be killed , the trauma that Kanae was exposed to

  And still, the resolution of the fiscal was that you went to your house taking her with you
  """

  show alan old speaking

  alan """
  After all that, you had an incomprehensible fate when you escape from the hospital after the accident

  Nurses didn't saw you. You passed by like nothing with officers next to you

  There was a mirror covering you that reflected nothing, you were invisible
  """

  show alan old serious
  show my_kanae worried soft

  alan """
  Then you disappeared again, and you infiltrated the hospital that you already had escaped without any problem, bypassing officers in all the surrounding areas.
  """

  show alan old speaking

  alan """
  I'm not an idiot. Not only you do have a special ability

  Something is moving threads and we are acting like simple puppets for it's entertainment

  This is beyond us, as you said.

  And is not just for you. We're just pawns in a bigger game.
  """

  show alan old serious

  alan """
  I don't have to be a total genius and expert in physics to realize how situations have been coupled for you to act this way

  As the world has aligned so that your lives becomes a disaster, but letting you to keep going
  """

  """
  Is everything that has happened to us so obvious that even Alan notices?
  """

  show alan old normal

  me """
  That's our theory, Alan. That's what we think.

  That \"thing\", as you name her, is also Kanae.
  """

  show alan old speaking

  alan """
  No, she is not.

  I don't know how you got all this, the only thing I know is that all this started because of you.
  """

  show alan old normal

  me """
  Listen, I know that's how it seems like, but there is something else beyond us

  He watches us

  He was the one who gave us this curse for some reason

  He, Controlling the whole flow of time with our actions in it, has structured the whole scenario for us to end up here.
  """

  show my_kanae blushed

  my_kanae "He is everything, Professor."

  show my_kanae worried soft

  my_kanae """
  He is everything. He sees everything

  I saw him. When my other self looked for me.

  He knew that she was going to do and allowed me to return
  """

  show alan old serious

  show my_kanae surprised

  my_kanae """
  He wasn't expecting you, Professor

  That surprised him for the first time ever. You were his first surprise.
  """

  if day5_iteration > 3:
    my_kanae """
    Your persinstence although

    He didn't like it.

    He just wanted to see up close for a moment, but you insisted more than you should.
    """
  else:
    my_kanae """
    You appeared from nowhere. He didn't knew you would be with us back in the void.
    """

    me "He was that Iluminated shadow, isn't it?"

    my_kanae """
    You felt him. Right in your chest.

    Your bones remember him. Your mind will not forget him.
    """

  show my_kanae worried soft
  
  me "Why us?"
  me "Why me?"

  show alan old speaking
  alan """
  It doesn't matter who has played with whom

  If he's a god of who knows what the hell and he got tired of seeing and doing nothing. That doesn't matter anymore
  """

  show alan old serious
  alan """
  If he has already seen everything, if he knows what's going to happen and, even so, you surprised him

  He will not let you die here, will he?
  """

  show alan old smiling
  alan """
  We should try it
  """

  show alan old speaking
  alan """
  I can't let you go

  If you are the only difference in this game, you are too dangerous to be on the outside, even more knowing that you are destroying the world by a damn being from another dimension.

  So, with that in mind, comes my question about you and the other monster
  """

  show alan old serious

  alan """
  If you aren't constant in time, it'd be foolish to kill that thing that is with you.

  You are the variable. 
  """

  show alan old speaking

  alan """
  If I kill her, she is just a companion for your travel
  """

  show my_kanae surprised

  alan """
  But if I kill you, I would make that being act more impulsively since you are what he is looking for.
  """

  show alan old serious
  alan """
  He wants changes, he wants something different

  He wants to know nothing

  And of course, he must know what will happen here, right?
  """

  """
  Would that be bad?

  I am just a toy for this being

  In all this situation, I've just destroyed so many lives.

  Kyoko, Makawa, Itawa...

  Kanae...
  """

  me """
  The void is a tunnel with an infinity of roads in all directions of space and time.

  This could be the most coveted desire for anyone, but the price he put for this was to high.
  """

  show my_kanae worried soft

  if day1_stabbed:
    "Right now, the wound that Makawa did to me started to appear as if it was done just today."

  me "We can be more."

  alan "The hell are you talking about?"

  show alan old speaking

  alan """
  You'll see. I had to do a lot of research to understand what was happening with you

  Since the accident on the road, until you arrived here today. I didn't stop investigating.
  """

  show alan old serious
  show my_kanae surprised

  alan """
  The only explanation I found for the monster that accompanies you has appeared, is that she doesn't belong to this line.

  This timeline is different from where it comes from. Or am I wrong?
  """

  """
  I can't say anything. Alan is saying all that has happened decoding all the events.
  """

  show alan old speaking
  show my_kanae worried soft

  alan """
  But of course, there was something else

  Something that was never mentioned anywhere. Your cicatrized rings.

  I saw the recordings when you where in the police station, where Kanae pointed at you that ring.
  """

  me "We... we have tried to understand this."
  alan "Yeah? How?"
  me "We had someone with us who was helping to understand"
  show alan old serious
  alan "Oyab, isn't it?"
  me "Yes."
  show alan old speaking
  alan "Question... where's he?"

  me "He was with us..."

  show alan old normal

  alan "We saw him in the hospital"

  show alan old speaking
  alan """
  He escaped with you
  """
  show alan old serious

  if day5_break_concrete:
    alan """
    Then you destroyed an entire highway

    Killed more than twelve people in one of the mayor accidents this city has seen, and then you dissapeared.
    """

    me "I... never thought that would cause that much damage"

    show alan old speaking

    alan """
    You destroyed an entire highway, what did you expected?

    There were a lot of injuried, a lot of deaths.
    """
  else:
    alan """
    When you killed one of my mens. You stopped his bike in the instant.

    He died at the moment of the impact, you know?
    """

    "I felt the ardor in my ring in the exact moment he crashed."

    show alan old speaking

    alan "You destroyed his family's life. But that doesn't matter, because this is \"bigger\" isn't it?"

  menu:
    " "
    "If you wouldn't had chased me":
      $ day6_rancor = True

    "I am sorry":
      $ day6_rancor = False

  show alan old serious

  if day6_rancor:
    alan "You are as stupid as you look"
  else:
    alan "It doesn't matter now. If you were able to reverse time, you would've done it already."

  show alan old speaking
  alan """
  Returning to the point, where is that guy Oyab?
  """

  stop music fadeout 3.0

  me """
  I can't remember...

  I just know that we jumped while I was exhausted.

  We where in the middle of the road.

  I remember the car... is coming in our way

  Oh fuck, we jumped and...
  """

  show alan old serious
  show my_kanae surprised

  alan "Paradox."

  me "What?"

  alan "You continued in a loop that started because of you"

  show my_kanae worried soft

  my_kanae "It can't be a Paradox. We have multiple lines. And in this one it was different."

  show alan old sigh

  alan "That's true."

  "That's Kanae?"

  show alan old speaking

  alan """
  You have unstructured our timeline in different portions, I see that. 

  You are a constant in this sea of variables, but that is under our view
  """

  show alan old serious
  alan """
  Still, Kanae seems to have a bigger connection with this being, but you look more important than any of them.

  Why is that? why your ring is still getting marked and what it mean?
  """

  show alan old normal

  """
  I had totally forgotten the ring timer, it can't be. 

  Alan said that Kanae ended the lives of a few of his friends, that restarts her time, but I've been fainted since the last jump

  If I don't do something, any of them couldâ€¦
  """

  show my_kanae surprised
  my_kanae "If he doesn't kill someone by his own, then someone will die because he didn't do it."

  show alan old serious
  alan "What the hell are you saying?"
  my_kanae "The other me also has one"

  """
  Alan quickly goes to check on Kanae, looking at her mutilated hand and can see that she has even more rings in her hands. 
  """

  show alan old speaking
  alan "And you say that each space is a dead person?"

  show my_kanae worried soft
  my_kanae "Yes."

  show alan old serious

  alan """
  Fucking hell.
  """

  hide my_kanae worried soft
  with dissolve

  show alan old serious at center
  with move

  play music solo_music fadein 3.0 fadeout 3.0 loop

  alan """
  You are monsters, you've destroyed so many lives here

  I am not leaving you go free, not this time.
  """

  """
  Totally angry, he points a gun at my head determined to put an end to the whole situation
  """

  alan "Why shouldn't I end all this right now?"

  show alan old serious:
    xalign 0.8
  with move

  show my_kanae worried soft at center
  with dissolve

  my_kanae """
  Please, do not do it. I beg you. 

  It is not just for me or the Professor. He will not accept it.
  """

  play sound wapash

  show alan old speaking at center
  show my_kanae sad at left
  with MoveTransition(0.2)
  with hpunch


  hide my_kanae sad 
  with dissolve

  alan "Shut up!"

  """
  Should I stop him?

  I killed Makawa, destroyed Kanae's life.

  Kyoko also died because of me.
  """

  show alan old sigh

  alan "This ends now."

  """
  Alan puts the gun in my head
  """

  if day5_break_concrete:
    """
    All that people in the highway

    Fuck!
    """

    if day6_rancor:
      "Still if they wouldn't've chased me, the result would have been the same."
  else:

    """
    I couldn't handle all the situation back there in the hospital.

    Neither when we escaped
    """

  """
  I couldn't protect Oyab.

  Naila was alone all the time that we left.
  """

  if not day4_instinct:
    """
    Fuck 

    How we killed that person that she was taking care
    """

    if day4_they_suffered:
      "I remember her tears."
    else:
      "She knew what we did. Her tears were evident."

  """
  Itawa, Makawa...

  Am I the cause of this?
  """

  show alan old serious

  menu:
    " "
    "I am not":
      $ dead = False

    "Sacrifice":
      call day6_alan_kills_me(scenary=0) from _call_day6_alan_kills_me
      return

  """
  No..

  Seeing the expression completely terrified of Kanae, makes me see that this is not over.

  I focus all the energy I had and release it in a microsecond
  """

  show alan old speaking
  with hpunch
  with hpunch
  play sound earthquake fadein 2.0 fadeout 2.0 loop

  alan "What the hell is happening?"

  """
  This small spasm, in just seconds, causes a great earthquake with us as its epicenter
  """

  show my_kanae worried soft at left
  with dissolve

  show my_kanae angry at center
  with MoveTransition(0.2)
  with hpunch

  play sound wapash

  show alan old speaking at right
  with move
  with hpunch

  play sound earthquake fadein 2.0 fadeout 2.0 loop

  show my_kanae worried soft
  with None

  hide alan old speaking
  with Dissolve(0.1)

  me "Kanae!"

  show my_kanae surprised
  with hpunch
  with hpunch

  "The earthquake continues and the gun Alan had falls on the ground"

  my_kanae "Professor!"

  show alan old serious at right
  with dissolve
  show alan old serious at center
  with MoveTransition(0.2)
  with hpunch

  play sound wapash

  show my_kanae sad at left
  with move
  with hpunch

  play sound earthquake fadein 2.0 fadeout 2.0 loop

  hide my_kanae sad
  with Dissolve(0.1)
  with hpunch

  show alan old speaking 

  alan "This is your great plan!? A fucking earthquake!?"
  with hpunch

  alan "You are a threat, I can't let you get out of here."
  with hpunch

  alan "You will continue to bring disaster and death to the world, but I can change it."

  menu: 
    " "
    "Take gun":
      "I tried to take the gun he had, but he grabs me and throws me on the ground"
      with hpunch

      menu:
        "I barely can stand up and Alan is in front of me"

        "Fight back":

          call day6_alan_kills_me(scenary=2) from _call_day6_alan_kills_me_1
          return

        "Escape":

          "I hit his knees with a kick that allows me to stand up again."
          with hpunch

    "Avoid":
      "I avoid his fist while the earth keeps shaking around us."
      with hpunch

  me "Alan, stop this! You don't have to do it."

  show alan old serious

  alan "Shut up!"
  with hpunch
  play sound wind_crash

  menu:
    " "
    "Punch":

      play sound punch
      with hpunch

      "I manage to punch Alan's face with the hardest fist tha I've throw"

    "Take gun":

      "I try to take the gun, but Alan grabs it as well"
      call day6_alan_kills_me(scenary=1) from _call_day6_alan_kills_me_2
      return

  play sound earthquake fadein 2.0 fadeout 2.0 loop
  
  "The gun he had before starts rolling in the ground"

  show alan old serious at right:
    xalign 0.8
  with move
  show my_kanae angry at left:
    xalign 0.2
  with dissolve

  my_kanae """
  Stop!

  I do not know what is in my mind. There is too much noise

  I am not sure of anything anymore, but I will not let you hurt him.
  """

  stop sound fadeout 3.0

  show alan old sigh
  show my_kanae mad

  alan """
  Listen, girl. I understand that your mind may be confused, but killing me isn't going to solve anything.
  """

  show alan old serious

  alan """
  Do you remember the explosives in the whole house? I wasn't kidding

  I took very seriously what I said about not letting you go.
  """

  me "Kanae, you don't have to do it."
  me "Now we can control this."

  show my_kanae mad talking

  my_kanae "Ok, Professor. But I am not putting down the gun."

  show my_kanae mad

  """
  I take the opportunity to release the other Kanae

  Alan tied her really well..
  """

  show alan old sigh at center
  with move

  show kanae_alt normal at right:
    xalign 0.8
  with dissolve

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  An earthquake? How could you create it? You are supposed to be exhausted, Professor.
  """

  show kanae_alt normal
  with dissolve

  me "I am glad you are ok as well, Kanae."

  show kanae_alt surprised
  show my_kanae surprised
  with dissolve

  $ renpy.pause(delay=1.0)

  my_kanae "You are... you are me?"

  show kanae_alt speaking
  with dissolve

  kanae_alt "I know it is confusing, but you will understand."

  show kanae_alt normal
  with dissolve

  show alan old speaking

  alan """
  You are happy, isn't it? 

  Free, having killed all my friends

  Having destroyed the world and its temporal lines.
  """

  show alan old sigh

  alan """
  You could kill yourself
  """

  show alan old serious
  show my_kanae mad

  alan """
  Kill yourself and stop this whole sick cycle, but you've been so arrogant that you prefer to end the life of anyone who is in your way to follow this game
  """

  show my_kanae surprised

  me "We don't know what would happen to the one who ends his life."

  show alan old sigh

  me """
  Perhaps if I kill myself, this curse would jump to other person while trying to surprise the entity that has put us in this situation.

  As you said, for now I am the variable, but it can be anyone else.
  """

  show alan old speaking

  alan "You don't know that."

  me "That's why we can't do it."

  show alan old sigh
  alan "Your lives are an error."

  show kanae_alt focus
  with dissolve

  stop music fadeout 4.0

  $ renpy.pause(delay=0.5)

  show my_kanae worried soft
  my_kanae "I can know what you think"

  show alan old normal

  show kanae_alt normal
  with dissolve

  show my_kanae surprised
  my_kanae "Your mind is united to mine. He can not die, he would blow up the house."

  """
  How did they communicate?
  """

  play music dramatic fadein 3.0 fadeout 3.0 loop

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  I can make the jump

  If it is true that this house would fly if he dies, it does not matter at all because it would not affect us.

  He has been behind us since the beginning and, now knowing the truth, he omits what he has in front of his own nose to carry out his foolish revenge.
  """

  show my_kanae mad
  show kanae_alt normal
  with dissolve

  $ renpy.pause(delay=1.0)

  show alan old sigh
  with None

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  He does not realize that this goes beyond him, beyond us

  It would be a hindrance leaving him alive knowing that he will continue to hunt us
  """

  show kanae_alt normal
  with dissolve

  me "Kanae... That's not necessary"

  """
  I know that this Kanae is different, but I can't see her talking like that.
  """

  show alan old normal
  show my_kanae surprised
  with None

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  Your time is up, Professor

  You should do it. We have spent most of the day here and you have not restarted your clock. He will work.
  """

  show kanae_alt normal
  with dissolve

  show alan old speaking

  alan "You talk as if he belongs to you."

  show my_kanae angry
  my_kanae "Enough!"

  show kanae_alt surprised
  with dissolve

  show my_kanae mad talking

  my_kanae """
  You can't handle the Professor at your wish

  You come from another timeline, you cause an accident that takes me to a coma and now...
  """
  show kanae_alt normal
  with dissolve

  show my_kanae angry
  show alan old sigh

  my_kanae """
  Now you want to continue murdering just to restart a clock that in the end you don't know well how it works.
  """

  show my_kanae mad

  $ renpy.pause(delay=0.7)

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  You never knew that I was from another timeline

  Neither that I caused your accident, Alan did not knew it well. He has just discovered it now
  """

  play music solo_music fadein 4.0 fadeout 2.0 loop

  show kanae_alt normal
  with dissolve

  play sound wapash
  show alan old normal:
    xalign 0.3
  with MoveTransition(0.1)
  with hpunch

  show my_kanae surprised at left
  with MoveTransition(0.1)
  hide my_kanae surprised
  with dissolve

  play sound punch

  show alan old serious:
    xalign 0.7
  with MoveTransition(0.1)
  with hpunch

  show kanae_alt surprised at right
  with MoveTransition(0.1)
  hide kanae_alt surprised
  with dissolve

  show alan old serious at center
  with MoveTransition(0.5)

  me "Alan, stop!"

  """
  Alan takes advantage of the confusion that exists between the doppelgangers to attack who had him pointed with the weapon.
  """

  show alan old sigh
  with hpunch

  play sound punch

  """
  He gives me several punches in the face, leaving me quite stunned
  """

  show alan old serious at left
  with move

  hide alan old serious
  with dissolve
  with hpunch

  play sound punch

  """
  He starts holding me by the neck
  """

  show dark:
    xalign 0.5 yalign 0.5 alpha .2
  with Dissolve(3.0)

  """
  I can't breath
  """
  with hpunch
  play sound gun

  hide dark
  with Dissolve(1.0)

  """
  I feel Alan's arms release my neck little by little

  While he falls on the floor with a hole in his back spilling the blood he was trying to get out of me.
  """

  show my_kanae surprised at left:
    xalign 0.2
  with dissolve

  me "Kanae..."

  "I approach the paralyzed young girl and hold the weapon to try to relieve her of what she had done"

  my_kanae "Professor... I..."

  stop music fadeout 5.0

  me "It's ok, Kanae. Calm down. You are ok."

  "When I turn around to see Alan, all our surroundings"

  play sound delayed_explosion fadein 0.3
  show bg alan_house exploding
  with ImageDissolve(alan_house_exploding, 1.5)
  with hpunch

  show my_kanae halfdream at left:
    xalign 0.2
  with dissolve

  show kanae_alt surprised at right:
    xalign 0.8
  with Dissolve(0.1)

  "What? Everything is paralyzed"

  "Alan didn't lied, I can see the beginning of the explosion that he said it would happen if he died."

  show kanae_alt speaking
  with dissolve

  kanae_alt "I can not hold this moment very much, Professor. We must go."

  show kanae_alt normal
  with dissolve

  """
  I am closer to her than my Kanae, who was frozen in time
  """

  """
  The other Kanae couldn't make mine to react, so I take her of the hand to make the jump
  """

  show kanae_alt speaking
  with dissolve

  kanae_alt "She is not coming with us."
  play music dramatic fadein 3.0 fadeout 3.0 loop

  show kanae_alt normal
  with dissolve

  me "What are you talking about? She is Kanae as well... she is my Kanae."

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  She did not know that I was from another timeline

  She did not know about the accident

  And she have the facility to know what I think while I can not know what she thinks.
  """

  show kanae_alt normal
  with dissolve

  $ renpy.pause(delay=0.4)

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  This is not a connection for having shared an infinity in the vacuum to return her to her body

  Alan did not have all the time in the world to tell her things he did not even know. She do not fool me.
  """

  show kanae_alt normal
  with dissolve

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  I am sorry, Professor

  I know you will not agree, but it is something I must do
  """

  show kanae_alt focus
  with dissolve

  """
  Kanae generates a wave of wind as in the desert
  """

  show kanae_alt normal
  with dissolve
  with hpunch
  play sound wind_crash
  play sound glass_breaking

  """
  I feel that same blast hit me by the side that throws me strongly against the wall
  """

  show my_kanae worried soft
  with Dissolve(2.0)

  "Kanae?"

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  You do not suddenly have his powers, you have his abilities.

  No, that is not all coincidence, because you are him. It is not like this?

  You wanted to try more. See more closely what happened and now you are in this situation where I can finish you.
  """

  show kanae_alt normal
  with dissolve

  me """
  Kanae, please. Stop this madness.

  She can't be him. She just recovered from the hospital.
  """

  show kanae_alt speaking
  with dissolve

  kanae_alt """
  You have no idea what I have done and whom I have sacrificed to continue this battle that you put on me since that day, but now you are in my hands
  """

  show kanae_alt normal
  with dissolve

  """
  No, I can't see this happening now.

  An explosion is paralyzed in time around us, while the person that I've tried to protect since it started all this is being threatened again

  This time is someone much more dangerous than I didn't wanted to face, but I had to do it, even with the accusations that she throws

  I stand up again, with the gun I had previously taken after Kanae shot Alan

  All the time begins to unbalance by the power that the other Kanae had used
  """

  show my_kanae worried soft:
    xalign 0.4
  show kanae_alt normal:
    xalign 0.6
  with MoveTransition(1.0)

  """
  She grabs her by the neck, it seems that, she is not frozen in time as our surroundings, but can't move at will
  """

  show my_kanae surprised
  show kanae_alt speaking
  with dissolve

  kanae_alt """
  I waited a lot this moment, you can not imagine how much.

  Or perhaps you do, because you knew everything up to this point, right?
  """

  show kanae_alt normal
  with dissolve

  me "Kanae, stop it. Please"

  me "I'm not gonna let you hurt her."

  """
  It was an empty threat trying to make her set free her other version.
  """

  show kanae_alt speaking
  show my_kanae worried soft
  with dissolve

  kanae_alt """
  Do not you realize, Professor?

  This is our chance to finish everything

  We both know that we end up in this house due to coincidences.

  Coincidences that were planned in advance by this bastard who put us in his sickly game, and now by his own curiosity is in our hands.
  """

  show kanae_alt normal
  with dissolve

  me "No! She can't be him. He could be able to escape from you then."

  show my_kanae sad
  with dissolve

  my_kanae "Help me, Professor... do not leave me"

  """
  My Kanae says with her voice cracked
  """

  menu:
    "I have the gun I took from her after she killed Alan..."

    "Shoot to Stop Kanae":
      call day6_shoot from _call_day6_shoot

      if dead:
        return

    "Convince her":
      $ dead = False

    "Easy way out" if not day4_instinct:
      call day6_easy_way_out from _call_day6_easy_way_out
      return

  if not day6_shoot:
    me """
    Kanae, I know that it seems that he is inside her, but that can't be.

    He would've escaped from you already. You know it better than anyone.
    """

  show kanae_alt speaking
  with dissolve

  kanae_alt "But Professor, there are so many things that does not fit with her."

  show kanae_alt normal
  with dissolve

  me "Perhaps, but we don't know if that's because of what we did in the hospital."

  my_kanae "T{w=0.1}h{w=0.1}a{w=0.1}n{w=0.1}k{w=0.1} y{w=0.1}o{w=0.1}u{w=0.1} f{w=0.1}o{w=0.1}r{w=0.1} d{w=0.1}e{w=0.1}f{w=0.1}e{w=0.1}n{w=0.1}d{w=0.1}i{w=0.1}n{w=0.1}g{w=0.1} m{w=0.1}e, Pro{w=0.1}fe{w=0.1}ssor"

  stop music fadeout 2.0

  show my_kanae normal
  show kanae_alt surprised
  with dissolve

  my_kanae "But{w=0.3} I think I can defend myself now."

  "What? No, no... Can't be."

  me "Kanae?"

  my_kanae """
  It has been entertaining to have you test yourself.

  It took me a lot to get to this result, but including you was the best thing

  You were what I couldn't control most of the time. The variable.
  """

  show kanae_alt loud speaking
  with dissolve

  kanae_alt "I knew it! It is her!{nw}"
  with hpunch
  play sound wapash

  show kanae_alt sad
  with Dissolve(0.1)

  show my_kanae speaking

  my_kanae "Quiet"

  """
  I can't move...
  """

  me "Kanae... why?"

  show my_kanae normal

  my_kanae """
  That day in Kanae's room. I had seen it so many times.
  """

  show my_kanae speaking

  my_kanae """
  {b}Times{/b}... hahaha
  """

  play music main fadein 3.0 fadeout 3.0 loop

  show my_kanae normal
  my_kanae """
  You'll see... I wanted to do something more with this... world. Something else.
  """

  show my_kanae surprised

  my_kanae """
  Doesn't matter how many times I switched the position of the elements inside of it, all the results were predictibles.
  """
  
  show my_kanae normal

  my_kanae """
  Then I thought, what would happen if some of the baenics had the ability to control a part of the renderization?
  """

  show my_kanae speaking

  my_kanae """
  You didn't dissapointed!
  """

  me "Renderization?"

  show my_kanae normal

  my_kanae "You are just the result of our creation, little Professor."

  me "Our creation?"
  me "What the hell are you talking about?"

  show my_kanae surprised

  my_kanae "Ah... And I thought you were a little smart. It seems that Kanae was the one with the ideas."

  me "Let her go."

  show my_kanae normal

  my_kanae "Oh, I will...."
  my_kanae "But the explosion will consume you, and the entire experiment as well.."

  "What?"

  show my_kanae speaking

  my_kanae "Thank you, both of you, for your colaboration!"

  show my_kanae normal

  hide my_kanae normal
  with Dissolve(0.1)

  me "Wait! KANAE!"

  stop music fadeout 1.0

  show kanae_alt surprised
  with Dissolve(0.1)
  show kanae_alt loud speaking
  with Dissolve(0.1)
  show kanae_alt loud speaking at center
  with MoveTransition(0.1)

  kanae_alt "PROFESSOR TAKE MY HA{nw}"
  with hpunch
  play sound thunder

  scene bg flash
  with Dissolve(0.1)

  scene bg dark
  with Fade(6.0, 0.1, 1.5)

  "Did I died? Why is everything black? I feel I am inside the shadow. Or is this simply what comes next? Darkness..."

  $ renpy.pause(delay=2.0)

  call dayx from _call_dayx